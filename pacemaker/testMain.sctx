#resource "timing.h"
#hostcode-c "#include \"timing.h\""
#hostcode-c "#include <stdio.h>"
// https://us.nallen.me/cs303/
@ClocksUseSD
scchart pacemaker {
  
  // https://us.nallen.me/cs303/
  const int AVI_VALUE = 3 // 300
  const int AEI_VALUE = 8 // 800
  const int PVARP_VALUE = 5 // 50
  const int VRP_VALUE = 15 // 150
  const int LRI_VALUE = 10 // 950
  const int URI_VALUE = 9 //900
 

  input signal AS, VS
  signal VRP_ACTIVE, URI_ACTIVE, PVARP_ACTIVE, LRI_ACTIVE
  
  output signal AP, VP
  
  region "AVI"{
    clock x = 0
    initial state idle
        
    if AS || AP go to active
    
    state active{
      entry do x = 0
    }
    if VS || pre(VP) && !VRP_ACTIVE abort to idle
    if x >= `AVI_VALUE` && !URI_ACTIVE do VP go to idle
  }
  
  region "PVARP"{
    clock x = 0
    initial state idle
    if VS || VP go to active
    state active{
      entry do x = 0
      during do PVARP_ACTIVE
    }
    
    if x >= `PVARP_VALUE` go to idle
  }
  
  region "AEI"{
    clock x = 0
    
    initial state idle
    
    if VS || VP go to active
    
    state active{
      entry do x = 0
    }
    
    if AS && !PVARP_ACTIVE abort to idle
    if x >= `AEI_VALUE` do AP go to idle
   }
  
  region "Ventricle Actions" {
    initial state idle
    if VS || VP go to active
    state active {
      clock x = 0
      region "VRP" {
        initial state active {
          during do VRP_ACTIVE
        }
        if x >= `VRP_VALUE` go to done
        final state done
      }
    }
    join to idle
  }
  
    region "URI"{
    initial state idle
    if VS || VP go to URI_ACTIVE

    state URI_ACTIVE{
      clock x = 0
      during do URI_ACTIVE

    }
    if x >= URI_VALUE go to idle
 
  }
  
  region "LRI"{
    initial state idle
    if VP || VS go to LRI_ACTIVE
    state LRI_ACTIVE{
      clock x = 0
      region{
        initial state active{
//          entry do x = 0
          during do LRI_ACTIVE
        }
        if x >= `LRI_VALUE` go to done
          final state done
      }
    }
    if VP || VS abort to LRI_ACTIVE
    do VP join to LRI_ACTIVE
  }
}